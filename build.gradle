plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.sonarqube' version '2.8'
}

group 'ch.bildspur'
version '0.6.1'

sourceCompatibility = 11

wrapper {
    gradleVersion = '6.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.6.2')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.6.2')
    testImplementation('org.hamcrest:hamcrest:2.2')
}

configurations {
    jar.archiveName = "artnet4j-${version}.jar"
}

sonarqube {
  properties {
    property "sonar.projectKey", "rynr_artnet4j"
    property "sonar.organization", "rynr-github"
    property "sonar.host.url", System.getProperty("SONAR_HOST_URL")
    property "sonar.login", System.getProperty("SONAR_TOKEN")
  }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    archiveName = "artnet4j-${version}.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cansik/artnet4j")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    afterEvaluate {
        publications {
            gpr(MavenPublication) {
                from(components.java)
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "ArtNet for Java and Processing"
                    description = "Art-Net DMX over IP library for Java and Processing."
                    url = "https://github.com/cansik/artnet4j"
                }
            }
        }
    }
}